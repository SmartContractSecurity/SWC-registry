# Title 
Uninitialised Storage Pointer

## Relationships
[CWE-824: Access of Uninitialized Pointer](https://cwe.mitre.org/data/definitions/824.html)

## Description 
Uninitialised local storage variables can point to other unexpected storage variables in the contract, leading to intentional or unintentional vulnerabilities.

## Remediation
It is recommended to explicitly specify the data location `memory` or `storage` when dealing with complex types to ensure they behave as expected.

## References 
- [SigmaPrime - Unintialised Storage Pointers](https://github.com/sigp/solidity-security-blog#unintialised-storage-pointers-1)
## Contract Samples
### crypto_roulette.sol
```Solidity
/*
 * @source: https://github.com/thec00n/smart-contract-honeypots/blob/master/CryptoRoulette.sol
 */
pragma solidity ^0.4.19;

// CryptoRoulette
//
// Guess the number secretly stored in the blockchain and win the whole contract balance!
// A new number is randomly chosen after each try.
//
// To play, call the play() method with the guessed number (1-20).  Bet price: 0.1 ether

contract CryptoRoulette {

    uint256 private secretNumber;
    uint256 public lastPlayed;
    uint256 public betPrice = 0.1 ether;
    address public ownerAddr;

    struct Game {
        address player;
        uint256 number;
    }
    Game[] public gamesPlayed;

    function CryptoRoulette() public {
        ownerAddr = msg.sender;
        shuffle();
    }

    function shuffle() internal {
        // randomly set secretNumber with a value between 1 and 20
        secretNumber = uint8(sha3(now, block.blockhash(block.number-1))) % 20 + 1;
    }

    function play(uint256 number) payable public {
        require(msg.value >= betPrice && number <= 10);

        Game game;
        game.player = msg.sender;
        game.number = number;
        gamesPlayed.push(game);

        if (number == secretNumber) {
            // win!
            msg.sender.transfer(this.balance);
        }

        shuffle();
        lastPlayed = now;
    }

    function kill() public {
        if (msg.sender == ownerAddr && now > lastPlayed + 1 days) {
            suicide(msg.sender);
        }
    }

    function() public payable { }
}
```
